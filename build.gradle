buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.5.+'
  }
}
apply plugin: 'android'

dependencies {
  compile fileTree(dir: 'libs', include: '*.jar')
}

android {
  compileSdkVersion 18
  buildToolsVersion '18.1'

  sourceSets {
    main {
      manifest.srcFile 'src/main/AndroidManifest.xml'
      java.srcDirs = ['src/main/java']
      resources.srcDirs = ['src/main/java']
      aidl.srcDirs = ['src/main/java']
      renderscript.srcDirs = ['src/main/rs']
      res.srcDirs = ['src/main/res']
      assets.srcDirs = ['src/main/assets']
    }

    // Move the tests to tests/java, tests/res, etc...
    instrumentTest.setRoot('tests')

    // Move the build types to build-types/<type>
    // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
    // This moves them out of them default location under src/<type>/... which would
    // conflict with src/ being used by the main source set.
    // Adding new build types or product flavors should be accompanied
    // by a similar customization.
    debug.setRoot('build-types/debug')
    release.setRoot('build-types/release')
  }

  signingConfigs {
    debug {
      //storeFile file("debug.keystore")
    }
    release {}
  }

  buildTypes {
    debug {
      packageNameSuffix ".debug"
      versionNameSuffix " Debug"
    }

    release {
      runProguard true
      proguardFile getDefaultProguardFile('proguard-android.txt')
    }
  }

  if (project.hasProperty('tackmobileKeystoreAlias') &&
      project.hasProperty('tackmobileKeystorePass') &&
      project.hasProperty('tackmobileKeystore')) {
    android.buildTypes.release.signingConfig signingConfigs.release
    android.signingConfigs.release.storeFile = file(tackmobileKeystore)
    android.signingConfigs.release.storePassword = tackmobileKeystorePass
    android.signingConfigs.release.keyAlias = tackmobileKeystoreAlias
    android.signingConfigs.release.keyPassword = tackmobileKeystorePass
  }
}
